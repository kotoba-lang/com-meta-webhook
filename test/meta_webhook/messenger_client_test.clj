(ns meta-webhook.messenger-client-test
  (:require [clojure.test :refer [deftest is]]
            [clojure.string :as str]
            [meta-webhook.messenger-client :as mc]))

(defn- fake-io [status body]
  (let [calls (atom [])]
    {:calls calls
     :creds {:page-access-token "tok-abc"}
     :json-write pr-str
     :json-read  (fn [s] (read-string s))
     :http-fn    (fn [req] (swap! calls conj req) {:status status :body body})}))

(deftest send-message-posts-recipient-and-text
  (let [io (fake-io 200 (pr-str {:message_id "m1"}))
        out (mc/send-message! io {:recipient-id "U1" :text "hi"})]
    (is (= {:message_id "m1"} out))
    (let [{:keys [url body]} (first @(:calls io))]
      (is (str/includes? url "access_token=tok-abc"))
      (is (= {:recipient {:id "U1"} :message {:text "hi"} :messaging_type "RESPONSE"}
             (read-string body))))))

(deftest send-message-respects-custom-messaging-type
  (let [io (fake-io 200 (pr-str {}))]
    (mc/send-message! io {:recipient-id "U1" :text "hi" :messaging-type "MESSAGE_TAG"})
    (is (= "MESSAGE_TAG" (:messaging_type (read-string (:body (first @(:calls io)))))))))

(deftest send-message-returns-error-shape-on-failure
  (let [io (fake-io 400 "bad request")]
    (is (= {:ok false :status 400 :error "bad request"}
           (mc/send-message! io {:recipient-id "U1" :text "hi"})))))
